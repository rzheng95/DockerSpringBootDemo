plugins {
    id 'org.springframework.boot' version '2.7.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "com.palantir.docker" version '0.34.0'
    id 'com.palantir.docker-run' version '0.34.0'
    id 'pl.allegro.tech.build.axion-release' version '1.13.14' // for version control
    id 'java'

}

project.version = scmVersion.version

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

docker {
    // project.name is specified in settings.gradle
    name "${project.name}:${project.version}"
    // copy jar file to Docker container
    files "$buildDir/libs/${project.name}-${project.version}.jar"
    // create ARG(arguments) in Dockerfile
    buildArgs ([JAR_FILE: "${project.name}-${project.version}.jar"])
}

dockerRun {
    name "${project.name}"
    image "${project.name}:${project.version}"
    ports '8080:8080'
    clean true // when container is killed it should be removed
    daemonize false // all output should go to the console
}